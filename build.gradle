plugins {
    id 'java'
    id 'jacoco'
    id 'org.sonarqube' version '2.7'
}

group 'com.example'
version '1.0-SNAPSHOT'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = 'https://packages.confluent.io/maven/'
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

sonarqube {
    properties {
        property 'sonar.host.url', 'http://127.0.0.1:9000/'
        property 'sonar.verbose', 'true'
        property 'sonar.sourceEncoding', 'UTF-8'
        property 'sonar.projectKey', 'javasparkexecutor'
        property 'sonar.projectName', 'JavaSparkExecutor'
        property 'sonar.projectDescription', 'Standalone Java Spark Application'
        property 'sonar.projectVersion', '1.0-SNAPSHOT'
    }
}

ext {
    sparkVersion = '2.4.6'
    scalaVersion = '2.11'
    kafkaVersion = '5.1.1'
    lombokVersion = '1.18.12'
    junitVersion = '5.6.2'
    junitPlatform = '1.6.2'
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    implementation "org.apache.spark:spark-sql_${scalaVersion}:${sparkVersion}"
    implementation "org.apache.spark:spark-avro_${scalaVersion}:${sparkVersion}"
    implementation "org.apache.spark:spark-sql-kafka-0-10_${scalaVersion}:${sparkVersion}"
    implementation "io.confluent:kafka-avro-serializer:${kafkaVersion}"
    implementation "io.confluent:kafka-schema-registry:${kafkaVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testImplementation "org.junit.platform:junit-platform-runner:${junitPlatform}"
    testImplementation "io.confluent:kafka-schema-registry:${kafkaVersion}:tests"
    testImplementation "org.apache.kafka:kafka_${scalaVersion}:2.1.1:test"
    testImplementation "org.apache.kafka:kafka-clients:2.1.1:test"
    testImplementation 'org.apache.curator:curator-test:2.6.0'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}
